# ------------------------------------------------------------------
# Licensed under the MIT License. See LICENSE in the project root.
# ------------------------------------------------------------------

"""
    MaxLengthDiscretization(length)

TODO
"""
struct MaxLengthDiscretization{â„’<:Len} <: DiscretizationMethod
  length::â„’
  MaxLengthDiscretization(length::â„’) where {â„’<:Len} = new{float(â„’)}(length)
end

MaxLengthDiscretization(length) = MaxLengthDiscretization(addunit(length, u"m"))

function discretize(box::Box, method::MaxLengthDiscretization)
  sizes = ceil.(Int, _sides(box) ./ method.length)
  discretize(box, RegularDiscretization(sizes))
end

function discretize(segment::Segment, method::MaxLengthDiscretization)
  size = ceil(Int, _measure(segment) / method.length)
  discretize(segment, RegularDiscretization(size))
end

discretize(chain::Chain, method::MaxLengthDiscretization) =
  mapreduce(s -> discretize(s, method), merge, segments(chain))

# -----------------
# HELPER FUNCTIONS
# -----------------

_sides(box::Box{<:ð”¼}) = sides(box)

function _sides(box::Box{<:ðŸŒ})
  A, B = extrema(box)
  a = convert(LatLon, coords(A))
  b = convert(LatLon, coords(B))
  P = withcrs(box, (a.lat, b.lon), LatLon)

  AP = Segment(A, P)
  PB = Segment(P, B)
  (_measure(AP), _measure(PB))
end

_measure(segment::Segment{<:ð”¼}) = measure(segment)

# TODO: Haversine returns the shortest distance between two points
# this is not always equal to the distance between two directed points
function _measure(segment::Segment{<:ðŸŒ})
  T = numtype(lentype(segment))
  ðŸŒŽ = ellipsoid(datum(crs(segment)))
  r = numconvert(T, majoraxis(ðŸŒŽ))

  A, B = extrema(segment)
  evaluate(Haversine(r), A, B)
end
