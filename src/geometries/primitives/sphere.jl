# ------------------------------------------------------------------
# Licensed under the MIT License. See LICENSE in the project root.
# ------------------------------------------------------------------

"""
    Sphere(center, radius)

A sphere with `center` and `radius`.

See also [`Ball`](@ref).
"""
struct Sphere{M<:Manifold,C<:CRS,â„’<:Len} <: Primitive{M,C}
  center::Point{M,C}
  radius::â„’
  Sphere(center::Point{M,C}, radius::â„’) where {M<:Manifold,C<:CRS,â„’<:Len} = new{M,C,float(â„’)}(center, radius)
end

Sphere(center::Point, radius) = Sphere(center, addunit(radius, u"m"))

Sphere(center::Tuple, radius) = Sphere(Point(center), radius)

Sphere(center::Point) = Sphere(center, oneunit(lentype(center)))

Sphere(center::Tuple) = Sphere(Point(center))

"""
    Sphere(p1, p2, p3)

A 2D sphere passing through points `p1`, `p2` and `p3`.
"""
function Sphere(p1::Point, p2::Point, p3::Point)
  x1, y1 = p2 - p1
  x2, y2 = p3 - p2
  c1 = centroid(Segment(p1, p2))
  c2 = centroid(Segment(p2, p3))
  l1 = Line(c1, c1 + Vec(y1, -x1))
  l2 = Line(c2, c2 + Vec(y2, -x2))
  center = l1 âˆ© l2
  radius = norm(center - p2)
  Sphere(center, radius)
end

Sphere(p1::Tuple, p2::Tuple, p3::Tuple) = Sphere(Point(p1), Point(p2), Point(p3))

"""
    Sphere(p1, p2, p3, p4)

A 3D sphere passing through points `p1`, `p2`, `p3` and `p4`.
"""
function Sphere(p1::Point, p2::Point, p3::Point, p4::Point)
  v1 = p1 - p4
  v2 = p2 - p4
  v3 = p3 - p4
  V = volume(Tetrahedron(p1, p2, p3, p4))
  râƒ— = ((v3 â‹… v3) * (v1 Ã— v2) + (v2 â‹… v2) * (v3 Ã— v1) + (v1 â‹… v1) * (v2 Ã— v3)) / 12V
  center = p4 + Vec(râƒ—)
  radius = norm(râƒ—)
  Sphere(center, radius)
end

Sphere(p1::Tuple, p2::Tuple, p3::Tuple, p4::Tuple) = Sphere(Point(p1), Point(p2), Point(p3), Point(p4))

paramdim(::Type{<:Sphere{ð”¼{Dim}}}) where {Dim} = Dim - 1

paramdim(::Type{<:Sphere{ðŸŒ}}) = 1

center(s::Sphere) = s.center

radius(s::Sphere) = s.radius

==(sâ‚::Sphere, sâ‚‚::Sphere) = sâ‚.center == sâ‚‚.center && sâ‚.radius == sâ‚‚.radius

Base.isapprox(sâ‚::Sphere, sâ‚‚::Sphere; atol=atol(lentype(sâ‚)), kwargs...) =
  isapprox(sâ‚.center, sâ‚‚.center; atol, kwargs...) && isapprox(sâ‚.radius, sâ‚‚.radius; atol, kwargs...)

(s::Sphere)(uv...) = Ball(center(s), radius(s))(1, uv...)
