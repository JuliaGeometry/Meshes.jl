# ------------------------------------------------------------------
# Licensed under the MIT License. See LICENSE in the project root.
# ------------------------------------------------------------------

"""
    CylinderSurface(bottom, top, radius)

A circular cylinder surface embedded in RÂ³ with given `radius`,
delimited by `bottom` and `top` planes.

    CylinderSurface(start, finish, radius)

Alternatively, construct a right circular cylinder surface with given `radius`
along the segment with `start` and `finish` end points.

    CylinderSurface(start, finish)

Or construct a right circular cylinder surface with unit radius along the segment
with `start` and `finish` end points.

    CylinderSurface(radius)

Finally, construct a right vertical circular cylinder surface with given `radius`.

See <https://en.wikipedia.org/wiki/Cylinder>. 
"""
struct CylinderSurface{C<:CRS,P<:Plane{C},â„’<:Len} <: Primitive{ð”¼{3},C}
  bot::P
  top::P
  radius::â„’
  CylinderSurface(bot::P, top::P, radius::â„’) where {C<:CRS,P<:Plane{C},â„’<:Len} = new{C,P,float(â„’)}(bot, top, radius)
end

CylinderSurface(bot::P, top::P, radius) where {P<:Plane} = CylinderSurface(bot, top, addunit(radius, u"m"))

function CylinderSurface(start::Point, finish::Point, radius)
  dir = finish - start
  bot = Plane(start, dir)
  top = Plane(finish, dir)
  CylinderSurface(bot, top, radius)
end

CylinderSurface(start::Tuple, finish::Tuple, radius) = CylinderSurface(Point(start), Point(finish), radius)

CylinderSurface(start::Point, finish::Point) = CylinderSurface(start, finish, oneunit(lentype(start)))

CylinderSurface(start::Tuple, finish::Tuple) = CylinderSurface(Point(start), Point(finish))

function CylinderSurface(radius)
  z = zero(radius)
  o = oneunit(radius)
  CylinderSurface(Point(z, z, z), Point(z, z, o), radius)
end

paramdim(::Type{<:CylinderSurface}) = 2

radius(c::CylinderSurface) = c.radius

bottom(c::CylinderSurface) = c.bot

top(c::CylinderSurface) = c.top

axis(c::CylinderSurface) = Line(c.bot(0, 0), c.top(0, 0))

function isright(c::CylinderSurface)
  â„’ = lentype(c)
  T = numtype(â„’)
  # cylinder is right if axis
  # is aligned with plane normals
  a = axis(c)
  d = a(T(1)) - a(T(0))
  v = normal(c.bot)
  w = normal(c.top)
  isparallelv = isapproxzero(norm(d Ã— v))
  isparallelw = isapproxzero(norm(d Ã— w))
  isparallelv && isparallelw
end

==(câ‚::CylinderSurface, câ‚‚::CylinderSurface) = câ‚.bot == câ‚‚.bot && câ‚.top == câ‚‚.top && câ‚.radius == câ‚‚.radius

Base.isapprox(câ‚::CylinderSurface, câ‚‚::CylinderSurface; atol=atol(lentype(câ‚)), kwargs...) =
  isapprox(câ‚.bot, câ‚‚.bot; atol, kwargs...) &&
  isapprox(câ‚.top, câ‚‚.top; atol, kwargs...) &&
  isapprox(câ‚.radius, câ‚‚.radius; atol, kwargs...)

function (c::CylinderSurface)(Ï†, z)
  â„’ = lentype(c)
  T = numtype(â„’)
  if (Ï† < 0 || Ï† > 1) || (z < 0 || z > 1)
    throw(DomainError((Ï†, z), "c(Ï†, z) is not defined for Ï†, z outside [0, 1]Â²."))
  end
  t = top(c)
  b = bottom(c)
  r = radius(c)
  a = axis(c)
  d = a(T(1)) - a(T(0))
  h = norm(d)
  o = centroid(c)

  # rotation to align z axis with cylinder axis
  Q = urotbetween(d, Vec(zero(â„’), zero(â„’), oneunit(â„’)))

  # new normals of planes in new rotated system
  náµ¦ = Q * normal(b)
  nâ‚œ = Q * normal(t)

  # given cylindrical coordinates (r*cos(Ï†), r*sin(Ï†), z) and the
  # equation of the plane, we can solve for z and find all points
  # along the ellipse obtained by intersection
  rsÏ†, rcÏ† = r .* sincospi(2 * T(Ï†))
  záµ¦ = -h / 2 - (rcÏ† * náµ¦[1] + rsÏ† * náµ¦[2]) / náµ¦[3]
  zâ‚œ = +h / 2 - (rcÏ† * nâ‚œ[1] + rsÏ† * nâ‚œ[2]) / nâ‚œ[3]
  páµ¦ = Point(rcÏ†, rsÏ†, záµ¦)
  pâ‚œ = Point(rcÏ†, rsÏ†, zâ‚œ)

  p = páµ¦ + T(z) * (pâ‚œ - páµ¦)
  o + Q' * to(p)
end

function hasintersectingplanes(c::CylinderSurface)
  x = c.bot âˆ© c.top
  !isnothing(x) && evaluate(Euclidean(), axis(c), x) < c.radius
end
