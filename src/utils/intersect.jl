# ------------------------------------------------------------------
# Licensed under the MIT License. See LICENSE in the project root.
# ------------------------------------------------------------------
"""
    bentleyottmann(segments; [digits])

Compute pairwise intersections between n `segments`
with `digits` precision in O(nâ‹…log(n)) time using
Bentley-Ottmann sweep line algorithm.

By default, set `digits` based on the absolute
tolerance of the length type of the segments.

## References

* Bentley & Ottmann 1979. [Algorithms for reporting and counting
  geometric intersections](https://ieeexplore.ieee.org/document/1675432)
"""
function bentleyottmann(segments; digits=_digits(segments))

  # orient segments
  segs = map(segments) do s
    a, b = coordround.(extrema(s), digits=digits)
    a > b ? (b, a) : (a, b)
  end

  pmin, pmax = segs |> boundingbox |> Stretch(1.05) |> extrema
  _, ymin = CoordRefSystems.values(coords(pmin))
  _, ymax = CoordRefSystems.values(coords(pmax))
  ybounds = (ymin, ymax)

  # retrieve types
  T = lentype(first(segs)[1])
  P = typeof(first(segs)[1])
  S = Tuple{P,P}
  U = Set{S}

  # initialization
  ğ’¬ = BinaryTrees.AVLTree{P,Tuple{U,U,U}}()
  â„› = BinaryTrees.AVLTree{_SweepSegment{P,T}}()
  lookup = Dict{S,Int}()
  # loop through segments and insert them into the event queue ğ’¬ with start and ending flags for the segments
  # additionally build the lookup table for segment indices
  for (i, (a, b)) in enumerate(segs)

    # add starting point and segment
    if !isnothing(BinaryTrees.search(ğ’¬, a))
      union!(BinaryTrees.value(BinaryTrees.search(ğ’¬, a))[1], U([(a, b)]))
    else
      BinaryTrees.insert!(ğ’¬, a, (U([(a, b)]), U(), U()))
    end

    # add ending point and segment
    if !isnothing(BinaryTrees.search(ğ’¬, b))
      union!(BinaryTrees.value(BinaryTrees.search(ğ’¬, b))[2], U([(a, b)]))
    else
      BinaryTrees.insert!(ğ’¬, b, (U(), U([(a, b)]), U()))
    end

    # lookup table for segment indices
    lookup[(a, b)] = i
  end

  # initialize sweepline
  sweepline = _SweepLine(pmin, ybounds)
  output = Dict{P,Vector{Int}}()
  # sweep line algorithm
  while !BinaryTrees.isempty(ğ’¬)
    # current point (or event)
    node = BinaryTrees.minnode(ğ’¬)
    p = BinaryTrees.key(node)
    # delete point from event queue
    BinaryTrees.delete!(ğ’¬, p)

    # beginning, ending, and crossing segments
    â„¬, â„°, â„³ = BinaryTrees.value(node)

    # crosses that arent endpoints (including them can lead to duplicates)
    â„³â‚š = setdiff(â„³, â„°)

    # handle status line
    # TODO: This is the primary bottleneck of our implementation. Resolve
    _handlestatus!(â„›, â„¬, â„³â‚š, â„°, sweepline, p)

    # build sorted bundle of segments
    bundle = @isdefined(bundle) ? (empty!(bundle); bundle) : Vector{_SweepSegment{P,T}}()
    for s in Iterators.flatten((â„¬, â„³â‚š))
      push!(bundle, _SweepSegment(s, sweepline))
    end
    sort!(bundle)

    # process bundled events
    if isempty(bundle)
      # if endpoint, check new adjacent segments
      sâ‚—, sáµ£ = BinaryTrees.prevnext(â„›, _SweepSegment(first(â„°), sweepline))
      isnothing(sâ‚—) || isnothing(sáµ£) || _newevent!(ğ’¬, sweepline, bundle, p, _keyseg(sâ‚—), _keyseg(sáµ£), digits)
    else

      # handle bottom and top events
      BinaryTrees.isempty(â„›) || _handlebottom!(bundle, â„›, ğ’¬, p, digits)

      BinaryTrees.isempty(â„›) || _handletop!(bundle, â„›, ğ’¬, p, digits)
    end

    # add necessary points and segment indices to output
    if !isempty(bundle) || !isempty(â„°)
      inds = Set{Int}()
      # for s in bundle# âˆª â„°â‚š
      for s in bundle
        push!(inds, lookup[_segment(s)])
      end
      for s in â„°
        push!(inds, lookup[s])
      end
      indáµ¥ = collect(inds)
      if haskey(output, p)
        union!(output[p], indáµ¥)
      else
        output[p] = indáµ¥
      end
    end
  end

  (collect(keys(output)), collect(values(output)))
end

##
## handling functions
##

function _handlestatus!(â„›, â„¬â‚š, â„³â‚š, â„°â‚š, sweepline, p)
  for s in â„°â‚š âˆª â„³â‚š
    segsweep = _SweepSegment(s, sweepline)
    BinaryTrees.delete!(â„›, segsweep)
  end

  # update sweepline
  sweepline.point = p

  for s in â„¬â‚š âˆª â„³â‚š
    BinaryTrees.insert!(â„›, _SweepSegment(s, sweepline))
  end
  nothing
end

function _handlebottom!(bundle, â„›, ğ’¬, p, digits)
  sâ€² = bundle[begin]

  sâ‚—, _ = !isnothing(sâ€²) ? BinaryTrees.prevnext(â„›, sâ€²) : (nothing, nothing)
  if !isnothing(sâ‚—)
    _newevent!(ğ’¬, _sweepline(sâ€²), bundle, p, _segment(sâ€²), _keyseg(sâ‚—), digits)
  end
end

function _handletop!(bundle, â„›, ğ’¬, p, digits)
  sâ€³ = bundle[end]

  _, sáµ¤ = !isnothing(sâ€³) ? BinaryTrees.prevnext(â„›, sâ€³) : (nothing, nothing)
  if !isnothing(sáµ¤)
    _newevent!(ğ’¬, _sweepline(sâ€³), bundle, p, _segment(sâ€³), _keyseg(sáµ¤), digits)
  end
end

# -----------------
# HELPER FUNCTIONS
# -----------------

function _newevent!(ğ’¬, sweepline, bundle, p, sâ‚, sâ‚‚, digits)
  intersection(Segment(sâ‚), Segment(sâ‚‚)) do I
    t = type(I)
    if t === Crossing || t === EdgeTouching
      i = coordround(get(I), digits=digits)
      if i â‰¥ p
        if i â‰ˆ p
          # this avoids repeatedly inserting the same intersection point
          push!(bundle, _SweepSegment(sâ‚‚, sweepline))
          push!(bundle, _SweepSegment(sâ‚, sweepline))
        else
          node = BinaryTrees.search(ğ’¬, i)
          # insert new event into the event queue
          if isnothing(node)
            BinaryTrees.insert!(ğ’¬, i, (Set{typeof(sâ‚)}(), Set{typeof(sâ‚)}(), Set([sâ‚, sâ‚‚])))
          else
            union!(BinaryTrees.value(node)[3], [sâ‚, sâ‚‚])
          end
        end
      end
    end
  end
end

# compute rounding digits for FP precision
function _digits(segments)
  s = first(segments)
  â„’ = lentype(s)
  Ï„ = ustrip(eps(â„’))
  round(Int, -log10(Ï„)) - 3
end

# convenience function to get the segment from the node
function _keyseg(segment)
  _segment(BinaryTrees.key(segment))
end

# ----------------
# DATA STRUCTURES
# ----------------

# tracks sweepline and current y position for searching
mutable struct _SweepLine{P<:Point,T<:Number}
  point::P
  ybounds::Tuple{T,T}
end
_sweeppoint(sweepline::_SweepLine) = getfield(sweepline, :point)
_sweepx(sweepline::_SweepLine) = CoordRefSystems.values(coords(_sweeppoint(sweepline)))[1]
_sweepy(sweepline::_SweepLine) = CoordRefSystems.values(coords(_sweeppoint(sweepline)))[2]
_sweepbounds(sweepline::_SweepLine) = getfield(sweepline, :ybounds)
Base.isless(a::_SweepLine{P,T}, b::_SweepLine{P,T}) where {P<:Point,T} = _sweeppoint(a) < _sweeppoint(b)

#= sweepline definition
This somewhat complicated definition is used to
handle intersections elegantly. It ensures that y coordinates
of non vertical segments are always correct unless overlapping,
vertical segments are always on top of the segments next to p,
ending segments don't intersect,
and all other segments are correctly ordered.
=#

function _sweepline(sweepline::_SweepLine)
  x = _sweepx(sweepline)
  y = _sweepy(sweepline)
  Ïµ = atol(x) + eps(x)
  lower, upper = _sweepbounds(sweepline)

  pâ‚ = Point(x + Ïµ, lower)
  pâ‚‚ = Point(x + Ïµ, y + Ïµ)
  pâ‚ƒ = Point(x - Ïµ, y + Ïµ)
  pâ‚„ = Point(x - Ïµ, upper)

  ((pâ‚, pâ‚‚), (pâ‚‚, pâ‚ƒ), (pâ‚ƒ, pâ‚„))
end

# sweepline intersection with segment
function _sweepintersect(seg::Tuple{P,P}, sweepline::_SweepLine{P,T}) where {P<:Point,T<:Number}
  rope = _sweepline(sweepline)
  I = nothing
  # this loop checks for intersections between the segment and the sweepline
  # Rope intersections are not type stable in this implementation, so this is a workaround
  for piece in rope
    I = intersection(Segment(seg), Segment(piece)) do I
      t = type(I)
      if t === Crossing || t === EdgeTouching || t === CornerTouching
        get(I)
      else
        nothing
      end
    end

    if !isnothing(I)
      break
    end
  end
  # happens if ending segment
  i = isnothing(I) ? seg[2] : I
  _, y = CoordRefSystems.values(coords(i))
  y
end

# takes input segment and attaches where it intersects sweepline
mutable struct _SweepSegment{P<:Point,T<:Number}
  const seg::Tuple{P,P}
  const sweepline::Base.RefValue{_SweepLine{P,T}}
  xintersect::T
end

# constructor for _SweepSegment
function _SweepSegment(seg::Tuple{P,P}, sweepline::_SweepLine{P,T}) where {P<:Point,T<:Number}
  y = _sweepintersect(seg, sweepline)
  ref = Base.RefValue{_SweepLine{P,T}}(sweepline)
  _SweepSegment{P,T}(seg, ref, y)
end

_segment(sweepsegment::_SweepSegment) = getfield(sweepsegment, :seg)
_xintersect(sweepsegment::_SweepSegment) = getfield(sweepsegment, :xintersect)
_sweepline(sweepsegment::_SweepSegment) = getfield(sweepsegment, :sweepline)[]
_sweeppoint(sweepsegment::_SweepSegment) = _sweeppoint(getfield(sweepsegment, :sweepline)[])

Base.:(==)(a::_SweepSegment, b::_SweepSegment) = _segment(a) == _segment(b)

function Base.isless(a::_SweepSegment{P,T}, b::_SweepSegment{P,T}) where {P<:Point,T}
  # if segments same, return false
  segâ‚, segáµ¦ = _segment(a), _segment(b)
  if isequal(segâ‚, segáµ¦)
    return false
  end
  # update to latest sweepline reference
  p = max(_sweeppoint(a), _sweeppoint(b))

  if p == segâ‚[1]
    s = _orientationplus(segáµ¦[1], segáµ¦[2], p; atol=ustrip(atol(T)))
  else
    s = 0
  end

  if s != 0 || _istrivial(segâ‚) || _istrivial(segáµ¦)
    return s < 0
  end

  # compute intersection over sweepline
  ya = _ycalc!(a)
  yb = _ycalc!(b)

  diff = ustrip(abs(ya - yb))
  tol = eps(T)
  # if segments are separated over y, check ya < yb
  if diff > tol
    ya < yb
  else
    # fallback to lexicographic ordering of segments
    segâ‚ < segáµ¦
  end
end

function _ycalc!(a::_SweepSegment{P,T}) where {P<:Point,T<:Number}
  # calculate y-coordinate of intersection with sweepline
  y = convert(T, _sweepintersect(_segment(a), _sweepline(a)))
  a.xintersect = y
  y
end

# function calculates the orientation while accounting for collinearity
# Use a tolerance for collinearity and orientation
function _orientationplus(A, B, C; atol=1e-12)
  o = iscollinear(A, B, C) ? 0 : (orientation(Ring(A, B, C)) == CCW ? 1 : -1)
  # if o â‰ˆ 0, say its collinear
  if o == 0 || isapprox(ustrip(signarea(A, B, C)), 0; atol=atol)
    0
  else
    o
  end
end

# handles  the degenerate case of segments that are trivial
_istrivial(s) = s[1] == s[2]
