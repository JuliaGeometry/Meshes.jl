# ------------------------------------------------------------------
# Licensed under the MIT License. See LICENSE in the project root.
# ------------------------------------------------------------------

"""
    bentleyottmann(segments; [digits])

Compute pairwise intersections between n `segments`
with `digits` precision in O((n+k)â‹…log(n)) time using
Bentley-Ottmann sweep line algorithm.

By default, set `digits` based on the absolute
tolerance of the length type of the segments.

## References

* Bentley & Ottmann 1979. [Algorithms for reporting and counting
  geometric intersections](https://ieeexplore.ieee.org/document/1675432)
"""
function bentleyottmann(segments; digits=_digits(segments))
  # orient segments and round coordinates
  segs = map(segments) do seg
    a, b = coordround.(extrema(seg), digits=digits)
    a > b ? (b, a) : (a, b)
  end

  # retrieve relevant types
  P = typeof(first(first(segs)))
  S = Tuple{P,P}

  # event queue: stores points with associated sets of starting, ending, and crossing segments
  ð’¬ = BinaryTrees.AVLTree{EventPoint{P,S}}()
  # lookup table mapping segments to their linear indices
  lookup = Dict{S,Int}()

  # add points and segments to event queue and lookup table
  buildqueue!(ð’¬, lookup, segs)

  # initialize sweepline
  sweep = SweepLine(segs)
  # output dictionary (planar graph ðº)
  ðº = Dict{P,Vector{Int}}()

  # handle events
  handle!(ðº, sweep, ð’¬, lookup, digits)

  (collect(keys(ðº)), collect(values(ðº)))
end

# -----------------------
# BentleyOttmann Handling
# -----------------------

function handle!(ðº, sweep, ð’¬, lookup, digits)
  # types
  P = typeof(sweep.p)
  â„’ = lentype(P)
  # vector holding segments intersecting the current event point
  bundle = Vector{SweepSegment{P,â„’}}()
  # holds segment indices for output
  inds = Set{Int}()

  _innerloops(ðº, sweep, ð’¬, lookup, bundle, inds, digits)
  ðº
end

function _innerloops(ðº, sweep, ð’¬, lookup, bundle, inds, digits)
  P = typeof(sweep.p)
  S = Tuple{P,P}
  â„¬â‚š, â„°â‚š, â„³â‚š = (Set{S}() for _ in 1:3)
  while !BinaryTrees.isempty(ð’¬)
    # pre allocate dictionary of intersection types
    # pull the next event point and associated data
    p, â„¬â‚š, â„°â‚š, â„³â‚š = _pullevent!(ð’¬, â„¬â‚š, â„°â‚š, â„³â‚š)

    # active segments at current event
    activesegs = activesegments(sweep)

    # update the status structure with the current event point
    _handlestatus!(activesegs, â„¬â‚š, â„³â‚š, â„°â‚š, sweep, p)

    # build bundle of segments crossing the current event point
    bundle = _buildbundle!(bundle, â„¬â‚š, â„³â‚š, sweep)
    sort!(bundle)

    _findintersections!(bundle, activesegs, ð’¬, p, â„°â‚š, sweep, digits)

    # add intersection points and corresponding segment indices to ðº
    _buildoutput!(ðº, bundle, inds, lookup, â„¬â‚š, â„°â‚š, p)
  end
  ðº
end

# ------------------------------------
# Status and event handlers
# ------------------------------------

# updates the status structure with the current event point
function _handlestatus!(activesegs, â„¬â‚š, â„³â‚š, â„°â‚š, sweep, p)
  for seg in Iterators.flatten((â„°â‚š, â„³â‚š))
    BinaryTrees.delete!(activesegs, SweepSegment(seg, sweep))
  end

  update!(sweep, p)

  for seg in Iterators.flatten((â„¬â‚š, â„³â‚š))
    BinaryTrees.insert!(activesegs, SweepSegment(seg, sweep))
  end
end

function _handlebottom!(bundle, activesegs, ð’¬, p, digits)
  # check segment below lowest
  sâ€² = bundle[begin]

  nsâ‚—, _ = !isnothing(sâ€²) ? BinaryTrees.prevnext(activesegs, sâ€²) : (nothing, nothing)
  if !isnothing(nsâ‚—)
    _newevent!(ð’¬, _sweepline(sâ€²), bundle, p, _segment(sâ€²), _keyseg(nsâ‚—), digits)
  end
end

function _handletop!(bundle, activesegs, ð’¬, p, digits)
  # check segment above highest
  sâ€³ = bundle[end]

  _, nsáµ¤ = !isnothing(sâ€³) ? BinaryTrees.prevnext(activesegs, sâ€³) : (nothing, nothing)
  if !isnothing(nsáµ¤)
    _newevent!(ð’¬, _sweepline(sâ€³), bundle, p, _segment(sâ€³), _keyseg(nsáµ¤), digits)
  end
end

# -----------------
# HELPER FUNCTIONS
# -----------------
# builds event queue
function buildqueue!(ð’¬, lookup, segs)
  for (i, seg) in enumerate(segs)
    _addpoint!(ð’¬, seg[1], [seg], :starts)
    _addpoint!(ð’¬, seg[2], [seg], :ends)
    lookup[seg] = i
  end
end

# obtains current event point and segments
function _pullevent!(ð’¬, â„¬â‚š, â„°â‚š, â„³â‚š)
  node = BinaryTrees.minnode(ð’¬)
  ep = BinaryTrees.key(node)
  p = ep.p
  S = typeof(â„¬â‚š)
  â„¬â‚š = haskey(ep.types, :starts) ? ep.types[:starts] : S()
  â„°â‚š = haskey(ep.types, :ends) ? ep.types[:ends] : S()
  â„³â‚š = haskey(ep.types, :crossings) ? ep.types[:crossings] : S()
  # crosses that aren't endpoints (including them can lead to duplicates)
  setdiff!(â„³â‚š, â„°â‚š)
  BinaryTrees.delete!(ð’¬, ep)

  p, â„¬â‚š, â„°â‚š, â„³â‚š
end

# builds bundle of segments crossing the current event point
function _buildbundle!(bundle, â„¬â‚š, â„³â‚š, sweep)
  empty!(bundle)
  for seg in Iterators.flatten((â„¬â‚š, â„³â‚š))
    push!(bundle, SweepSegment(seg, sweep))
  end
  bundle
end

# finds new intersections
function _findintersections!(bundle, activesegs, ð’¬, p, â„°â‚š, sweep, digits)
  if isempty(bundle) # occurs at endpoints
    endseg = SweepSegment(first(â„°â‚š), sweep) # segment at endpoint
    # check newly adjacent segments
    nsâ‚—, nsáµ¤ = BinaryTrees.prevnext(activesegs, endseg)
    isnothing(nsâ‚—) || isnothing(nsáµ¤) || _newevent!(ð’¬, sweep, bundle, p, _keyseg(nsâ‚—), _keyseg(nsáµ¤), digits)
  else
    # check for intersections with adjacent segments below and above the current event point
    BinaryTrees.isempty(activesegs) || _handlebottom!(bundle, activesegs, ð’¬, p, digits)
    BinaryTrees.isempty(activesegs) || _handletop!(bundle, activesegs, ð’¬, p, digits)
  end
end

# builds output for the current event point
function _buildoutput!(ðº, bundle, inds, lookup, â„¬, â„°, p)
  # add intersection points and corresponding segment indices to ðº
  if length(bundle) > length(â„¬) # bundle only has â„¬ unless p is an intersection
    # add start and crossing segments
    for s in bundle
      push!(inds, lookup[_segment(s)])
    end
    # add ending segments
    for seg in â„°
      push!(inds, lookup[seg])
    end

    # add indices to output
    indáµ¥ = collect(inds)
    if haskey(ðº, p)
      union!(ðº[p], indáµ¥)
    else
      ðº[p] = indáµ¥
    end
  end
  empty!(inds)
  nothing
end

# add new events to the event queue as needed
function _newevent!(ð’¬, sweep, bundle, p, segâ‚, segâ‚‚, digits)
  intersection(Segment(segâ‚), Segment(segâ‚‚)) do I
    t = type(I)
    if t === Crossing || t === EdgeTouching
      i = coordround(get(I), digits=digits)
      if i â‰ˆ p # helps with vertical+horizontal intersections
        push!(bundle, SweepSegment(segâ‚‚, sweep))
        push!(bundle, SweepSegment(segâ‚, sweep))
      elseif i > p # add to ð’¬, update existing point if needed
        _addpoint!(ð’¬, i, [segâ‚, segâ‚‚], :crossings)
      end
    end
  end
end

# add an endpoint and corresponding segment to the event queue at a given position (1=start, 2=end, 3=crossing)
function _addpoint!(ð’¬, p, segs, pos)
  P = typeof(p)
  S = Tuple{P,P}
  node = BinaryTrees.search(ð’¬, EventPoint(p, S))
  # updates or adds event point based on existing events in ð’¬
  if !isnothing(node)
    update!(node.key, segs, pos)
  else
    # create a new event point for given position
    BinaryTrees.insert!(ð’¬, EventPoint(p, segs, pos))
  end
end

# compute the number of significant digits based on the segment type
# this is used to determine the precision of the points
function _digits(segments)
  seg = first(segments)
  â„’ = lentype(seg)
  Ï„ = ustrip(eps(â„’))
  round(Int, 0.8 * (-log10(Ï„))) # 0.8 is a heuristic to avoid numerical issues
end

# compute y bounds of the segments
function _ybounds(segs)
  # compute bounding box
  bbox = boundingbox(segs)

  # stretch bounding bbox
  T = numtype(lentype(bbox))
  sbox = bbox |> Stretch(T(1.05))

  # extract y coordinate values
  map(p -> coords(p).y, extrema(sbox))
end

# convenience function to get the segment from a our AVLNode{SweepSegment} structure
_keyseg(ns) = _segment(BinaryTrees.key(ns))

# ----------------
# DATA STRUCTURES
# ----------------

# tracks the event point and constructs the sweepline lazily
mutable struct SweepLine{P<:Point,â„’<:Number}
  p::P
  const ybounds::Tuple{â„’,â„’}
  activesegments::Any # sorted sequence of segments currently intersecting the sweepline
end

# getters
_sweepcoords(sweep::SweepLine) = CoordRefSystems.values(coords(sweep.p))
activesegments(sweep::SweepLine) = sweep.activesegments
update!(sweep::SweepLine, p) = (sweep.p = p; sweep)

#= Sweepline Definition ---

defined as the line segments
|
â””â€”â€”
 o|
  |
where o is the sweepline point and lines are the sweepline

This definition is used to
handle intersections elegantly. It ensures that y coordinates
of non vertical segments are always correct,
vertical segments are always on top of non-vertical segments next to p,
ending segments don't intersect, and all other segments are correctly ordered.

Inspired by LEDA implementation, but modified for Julia
=#
function sweepline(sweep::SweepLine)
  x, y = _sweepcoords(sweep)
  # perturbation to avoid numerical issues
  Ïµ = atol(x) + eps(x)
  lower, upper = sweep.ybounds

  pâ‚ = Point(x + Ïµ, lower) # lowest point
  pâ‚‚ = Point(x + Ïµ, y + 2Ïµ) # doubled to avoid precision issues
  pâ‚ƒ = Point(x - Ïµ, y + 2Ïµ)
  pâ‚„ = Point(x - Ïµ, upper) # highest point
  # tuple of segments representing the sweepline
  # ((lower vertical), (horizontal), (upper vertical))
  ((pâ‚, pâ‚‚), (pâ‚‚, pâ‚ƒ), (pâ‚ƒ, pâ‚„))
end

# sweepline intersection with segment
function _sweepintersect(seg::Tuple{P,P}, sweep::SweepLine{P,â„’}) where {P<:Point,â„’<:Number}
  rope = sweepline(sweep)
  I = nothing
  # check for intersections between the segment and the sweepline.
  # `Rope()` intersections are not type stable; this loop is a workaround.
  for segáµ£ in rope
    I = intersection(Segment(seg), Segment(segáµ£)) do ð‘–
      t = type(ð‘–)
      (t === Crossing || t === EdgeTouching || t === CornerTouching) ? get(ð‘–) : nothing
    end
    if !isnothing(I)
      break
    end
  end

  # if I isn't found, then we are handling an ending segment, so seg[2] is used instead
  i = isnothing(I) ? seg[2] : I
  _, y = CoordRefSystems.values(coords(i))
  y
end

# tracks a segment and its intersection with the sweepline at the latest calculated event point
mutable struct SweepSegment{P<:Point,â„’<:Number}
  const seg::Tuple{P,P}
  const sweepline::SweepLine{P,â„’}
  yintersect::â„’ # information about the intersection with the sweepline
  latestpoint::P # latest point of the sweepline used to calculate the intersection
end

# constructor for SweepSegment using SweepLine
function SweepSegment(seg::Tuple{P,P}, sweep::SweepLine{P,â„’}) where {P<:Point,â„’<:Number}
  y = _sweepintersect(seg, sweep)
  SweepSegment{P,â„’}(seg, sweep, y, sweep.p)
end
# extend SweepLine for SweepSegment
function SweepLine(segs)
  P = typeof(first(first(segs)))
  â„’ = lentype(P)
  ybounds = _ybounds(segs)
  pmin, _ = first(extrema(segs))
  SweepLine{P,â„’}(pmin, ybounds, BinaryTrees.AVLTree{SweepSegment{P,â„’}}())
end

# getters for SweepSegment
_segment(s::SweepSegment) = getfield(s, :seg)
_sweepline(s::SweepSegment) = getfield(s, :sweepline)

Base.:(==)(sâ‚::SweepSegment, sâ‚‚::SweepSegment) = sâ‚.seg == sâ‚‚.seg

# compare two segments based on their sweepline intersection relative to the current event point.
function Base.isless(sâ‚::SweepSegment{P,â„’}, sâ‚‚::SweepSegment{P,â„’}) where {P<:Point,â„’}
  # if segments same, return false
  if sâ‚ == sâ‚‚
    return false
  end
  #* calculating the y intersect is the largest performance bottleneck
  ya = sâ‚.yintersect # sâ‚ is always up-to-date
  yb = _ycalc!(sâ‚‚)

  diff = ustrip(abs(ya - yb))
  tol = eps(â„’)
  # if segments are separated over y, check ya < yb
  if diff > tol
    ya < yb
  else
    # fallback to lexicographic ordering of segments
    segâ‚, segâ‚‚ = sâ‚.seg, sâ‚‚.seg
    segâ‚ < segâ‚‚
  end
end

# calculate y-coordinate of intersection with sweepline
function _ycalc!(s::SweepSegment{P,â„’}) where {P<:Point,â„’<:Number}
  sweep = s.sweepline
  # if the latest point is the sweepline point, use the precalculated intersection
  if s.latestpoint === sweep.p
    y = s.yintersect
  else
    # otherwise, calculate the intersection with the sweepline
    # and update
    y = convert(â„’, _sweepintersect(s.seg, sweep))
    s.latestpoint = sweep.p
    s.yintersect = y
  end
  y
end

struct EventPoint{P,S}
  p::P
  types::Dict{Symbol,Set{S}}
end

# empty point constructor
function EventPoint(p::P, ::Type{S}) where {P,S}
  types = Dict{Symbol,Set{S}}()
  EventPoint{P,S}(p, types)
end

function EventPoint(p::P, segs::Vector{S}, pos::Symbol) where {P,S}
  types = Dict{Symbol,Set{S}}()
  # Add the position symbol and set
  types[pos] = Set(segs)
  EventPoint{P,S}(p, types)
end

Base.isless(epâ‚::EventPoint, epâ‚‚::EventPoint) = epâ‚.p < epâ‚‚.p

function update!(ep::EventPoint{P,S}, segs, pos::Symbol) where {P,S}
  set = get!(ep.types, pos, Set{S}())
  foreach(s -> push!(set, s), segs)
end
