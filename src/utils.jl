# ------------------------------------------------------------------
# Licensed under the MIT License. See LICENSE in the project root.
# ------------------------------------------------------------------

# auxiliary type for dispatch purposes
const GeometryOrDomain{Dim} = Union{Geometry{Dim},Domain{Dim}}

"""
    fitdims(dims, D)

Fit tuple `dims` to a given length `D` by repeating the last dimension.
"""
function fitdims(dims::Dims{N}, D) where {N}
  ntuple(i -> i â‰¤ N ? dims[i] : last(dims), D)
end

"""
    collectat(iter, inds)

Collect iterator `iter` at indices `inds` without materialization.
"""
function collectat(iter, inds)
  if isempty(inds)
    eltype(iter)[]
  else
    selectat(inds) = enumerate â¨Ÿ TakeWhile(x -> first(x) â‰¤ last(inds)) â¨Ÿ Filter(y -> first(y) âˆˆ inds) â¨Ÿ Map(last)
    iter |> selectat(inds) |> tcollect
  end
end

"""
    signarea(A, B, C)

Compute signed area of triangle formed by points `A`, `B` and `C`.
"""
function signarea(A::Point{2}, B::Point{2}, C::Point{2})
  ((B - A) Ã— (C - A)) / 2
end

"""
    householderbasis(n)

Returns a pair of orthonormal tangent vectors `u` and `v` from a normal `n`,
such that `u`, `v`, and `n` form a right-hand orthogonal system.

## References

* D.S. Lopes et al. 2013. ["Tangent vectors to a 3-D surface normal: A geometric tool
  to find orthogonal vectors based on the Householder transformation"]
  (https://doi.org/10.1016/j.cad.2012.11.003)
"""
function householderbasis(n::Vec{3,L}) where {L}
  nÌ‚ = norm(n)
  h = n .+ nÌ‚
  i = argmax(h)
  h = Vec(ntuple(j -> j == i ? h[i] : zero(L), 3))
  H = I - 2h * transpose(h) / (transpose(h) * h)
  u, v = [H[:, j] for j in 1:3 if j != i]
  i == 2 && ((u, v) = (v, u))
  Vec(u), Vec(v)
end

"""
    svdbasis(points)

Returns the 2D basis that retains most of the variance in the list of 3D `points`
using the singular value decomposition (SVD).

See <https://math.stackexchange.com/a/99317>.
"""
function svdbasis(p::AbstractVector{Point{3}})
  X = reduce(hcat, coordinates.(p))
  Î¼ = sum(X, dims=2) / size(X, 2)
  Z = X .- Î¼
  ğ’¬ = eltype(X)
  U = svd(ustrip(Z)).U * unit(ğ’¬)
  u = Vec(U[:, 1]...)
  v = Vec(U[:, 2]...)
  n = Vec(zero(ğ’¬), zero(ğ’¬), oneunit(ğ’¬))
  (u Ã— v) â‹… n < zero(ğ’¬) ? (v, u) : (u, v)
end

"""
    mayberound(Î», x, tol)

Round `Î»` to `x` if it is within the tolerance `tol`.
"""
function mayberound(Î»::T, x::T, atol=atol(T)) where {T}
  isapprox(Î», x, atol=atol) ? x : Î»
end

"""
    intersectparameters(a, b, c, d)

Compute the parameters `Î»â‚` and `Î»â‚‚` of the lines 
`a + Î»â‚ â‹… vâƒ—â‚`, with `vâƒ—â‚ = b - a` and
`c + Î»â‚‚ â‹… vâƒ—â‚‚`, with `vâƒ—â‚‚ = d - c` spanned by the input
points `a`, `b` resp. `c`, `d` such that to yield line
points with minimal distance or the intersection point
(if lines intersect).

Furthermore, the ranks `r` of the matrix of the linear
system `A â‹… Î»âƒ— = yâƒ—`, with `A = [vâƒ—â‚ -vâƒ—â‚‚], yâƒ— = c - a`
and the rank `râ‚` of the augmented matrix `[A yâƒ—]` are
calculated in order to identify the intersection type:

- Intersection: r == râ‚ == 2
- Colinear: r == râ‚ == 1
- No intersection: r != râ‚
  - No intersection and parallel:  r == 1, râ‚ == 2
  - No intersection, skew lines: r == 2, râ‚ == 3
"""
function intersectparameters(a::Point{Dim}, b::Point{Dim}, c::Point{Dim}, d::Point{Dim}) where {Dim}
  A = [ustrip(b - a) ustrip(c - d)]
  y = ustrip(c - a)
  T = eltype(A)

  # calculate the rank of the augmented matrix by checking
  # the zero entries of the diagonal of R
  _, R = qr([A y])

  # for Dim == 2 one has to check the L1 norm of rows as 
  # there are more columns than rows
  Ï„ = atol(T)
  râ‚ = sum(>(Ï„), sum(abs, R, dims=2))

  # calculate the rank of the rectangular matrix
  r = sum(>(Ï„), sum(abs, view(R, :, 1:2), dims=2))

  # calculate parameters of intersection or closest point
  if r â‰¥ 2
    Î» = A \ y
    Î»â‚, Î»â‚‚ = Î»[1], Î»[2]
  else # parallel or collinear
    Î»â‚, Î»â‚‚ = zero(T), zero(T)
  end

  Î»â‚, Î»â‚‚, r, râ‚
end

"""
    XYZ(xyz)

Generate the coordinate arrays `XYZ` from the coordinate vectors `xyz`.
"""
@generated function XYZ(xyz::NTuple{Dim,<:AbstractVector{T}}) where {Dim,T}
  exprs = ntuple(Dim) do d
    quote
      a = xyz[$d]
      A = Array{T,Dim}(undef, length.(xyz))
      @nloops $Dim i A begin
        @nref($Dim, A, i) = a[$(Symbol(:i_, d))]
      end
      A
    end
  end
  Expr(:tuple, exprs...)
end
